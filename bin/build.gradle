import groovy.xml.XmlUtil
import org.silverpeas.setup.configuration.JBossServer

description = 'Distribution of Silverpeas. Its goal is to manage the installation, the configuration and the execution of Silverpeas'
group = 'org.silverpeas'
version = '6.0-SNAPSHOT'

apply plugin: 'maven'
apply plugin: 'silverconf'

configurations {
  all {
    resolutionStrategy {
      cacheDynamicVersionsFor 0, 'seconds'
      cacheChangingModulesFor 0, 'seconds'
    }
  }
  silverpeas {
    description = 'Silverpeas Modules'
    transitive = true
  }
}

repositories {
  maven {
    url 'http://www.silverpeas.org/nexus/content/groups/silverpeas'
  }
  mavenLocal()
}

dependencies {
  silverpeas "org.silverpeas:silverpeas-assembly:${project.version}"
}

project.ext {
  jackrabbitJCAUri = 'http://www.silverpeas.org/files/jackrabbit-jca.rar'
  distDir = new File('dist', buildDir)
}

silverconf {
  modulesDir = "${silverconf.silverpeasHome}/modules"
  driversDir = "${buildDir}/drivers"
}

assemble {
  outputs.dir distDir
  outputs.dir file(new File(project.silverconf.driversDir))
  outputs.dir file(new File(project.silverconf.modulesDir))
  outputs.file file(new File('silverpeas.war', buildDir))
  doFirst {
    // fetch all the artifacts and explode some of them into the output directories
    def artifacts = configurations.silverpeas.files
    artifacts.each { a ->
      if (a.name.endsWith('.war')) {
        copy {
          from(zipTree(a)) {
            rename 'web.xml', "web-${a.name}.xml"
          }
          into distDir.path
        }
      } else if (a.name.endsWith('-configuration.jar')) {
        copy {
          from(zipTree(a))
          exclude '**/META-INF/**'
          into project.silverconf.silverpeasHome
        }
      } else if (a.name.startsWith('postgresql') || a.name.startsWith('jtds')) {
        // h2 is already provided by JBoss >= 8
        copy {
          from(file(a)) {
            rename a.name, a.name.substring(0, a.name.indexOf('-')) + '.jar'
          }
          into project.silverconf.driversDir
        }
      }
    }
    ant.get(src: jackrabbitJCAUri, dest: buildDir.path)
  }
  doLast {
    // merge all of the web.xml from the different WARs into a single one
    def mainWebXmlFileName = "web-war-core-${project.version}.war.xml"
    def mainWebXmlFile = new File("${distDir.path}/WEB-INF/${mainWebXmlFileName}")
    def mainWebXml = new XmlSlurper(false, false).parse(mainWebXmlFile)
    new File('WEB-INF', distDir).listFiles(new FilenameFilter() {
      boolean accept(File f, String fileName) {
        return fileName.startsWith('web-') && fileName.endsWith('war.xml') &&
            fileName != mainWebXmlFileName
      }
    }).each {
      // the sub-elements of the web-app elements can be in the arbitrary order.
      def aWebXml = new XmlSlurper(false, false).parse(it)
      aWebXml.'context-param'.each { elt -> mainWebXml.appendNode(elt) }
      aWebXml.'filter'.each { elt -> mainWebXml.appendNode(elt) }
      aWebXml.'listener'.each { elt -> mainWebXml.appendNode(elt) }
      aWebXml.'servlet'.each { elt -> mainWebXml.appendNode(elt) }
      aWebXml.'servlet-mapping'.each { elt -> mainWebXml.appendNode(elt) }
      aWebXml.'resource-env-ref'.each { elt -> mainWebXml.appendNode(elt) }
      GFileUtils.forceDelete(it)
    }
    XmlUtil.serialize(mainWebXml, new FileWriter("${distDir.path}/WEB-INF/web.xml"))
    GFileUtils.forceDelete(mainWebXmlFile)
    ant.zip(destfile: "${buildDir.path}/silverpeas.war") {
      fileset(dir: distDir.path)
    }
  }
}

task configure(dependsOn: [configureJBoss, configureSilverpeas]) {
  description 'Configures both JBoss and Silverpeas'
  group 'Build'
  doLast {
    println 'CONFIGURATION COMPLETE'
  }
}

/*
task database(dependsOn: configureSilverpeas, type: JavaExec) {
  description 'Install/Update the datasource used by Silverpeas'
  group 'Build'
  main = 'org.silverpeas.dbbuilder.DBBuilder'
  classpath = buildscript.configurations.classpath
  jvmArgs "-Dsilverpeas.home=${project.silverconf.silverpeasHome} -Ddbbuilder.home=${project.silverconf.silverpeasHome} -Ddbbuilder.data=${project.silverconf.silverpeasHome}/data"
}
*/

task deploy(dependsOn: configure) {
  description 'Installs the Silverpeas application'
  group 'Build'
  doLast {
    copy {
      from files(new File('silverpeas.war', buildDir), new File('jackrabbit-jca.rar', buildDir))
      into "${project.silverconf.jbossHome}/standalone/deployments"
    }
  }
}

task start {
  description 'Starts Silverpeas'
  group 'Silverpeas'
  doLast {
    new JBossServer(project.silverconf.jbossHome)
        .redirectOutputTo(new File("${silverconf.logDir}/output.log"))
        .start()
  }
}

task debug {
  description 'Starts Silverpeas in debug mode'
  group 'Silverpeas'
  doLast {
    new JBossServer(project.silverconf.jbossHome)
        .redirectOutputTo(new File("${silverconf.logDir}/output.log"))
        .debug()
  }
}

task stop {
  description 'Stops Silverpeas'
  group 'Silverpeas'
  doLast {
    new JBossServer(project.silverconf.jbossHome)
        .redirectOutputTo(new File("${silverconf.logDir}/output.log"))
        .stop()
  }
}

task status {
  description 'Checks the status of Silverpeas: is it running? is it well configured?'
  group 'Silverpeas'
  doLast {
    def jboss = new JBossServer(project.silverconf.jbossHome)
    String configurationStatus = (jboss.isAlreadyConfigured() ? 'Configured: [OK]' : 'Configured: [NOK]')
    String executionStatus = (jboss.isRunning() ? 'Running:    [OK]' : 'Running:    [NOK]')
    println configurationStatus
    println executionStatus
  }
}

task undeployAll(type: Delete) {
  description 'Undeploy all of the artefacts from the JBoss server (including Silverpeas)'
  group 'Silverpeas'
  doLast {
    fileTree(dir: "${project.silverconf.jbossHome}/standalone/deployments").exclude('README.txt').each {
      delete it
    }
  }
}



