import groovy.xml.XmlUtil
import org.silverpeas.setup.configuration.JBossServer

description = 'Distribution of Silverpeas. Its aim is to setup and to execute a Silverpeas installation'
group = 'org.silverpeas'
version = '6.0-SNAPSHOT'

apply plugin: 'maven'
apply plugin: 'silversetup'

configurations {
  /* necessary only when working with a SNAPSHOT version of this project
  all {
    resolutionStrategy {
      cacheDynamicVersionsFor 0, 'seconds'
      cacheChangingModulesFor 0, 'seconds'
    }
  }*/
  silverpeas {
    description = 'Silverpeas Modules'
    transitive = true
  }
}

repositories {
  maven {
    url 'http://www.silverpeas.org/nexus/content/groups/silverpeas'
  }
  mavenLocal()
}

dependencies {
  silverpeas "org.silverpeas:silverpeas-assembly:${project.version}"
}

buildscript {
  dependencies {
    classpath "org.silverpeas:silverpeas-setup:6.0-SNAPSHOT"
  }
  repositories {
    maven {
      url 'http://www.silverpeas.org/nexus/content/groups/silverpeas'
    }
    mavenLocal()
  }
}

/**
 * Global project properties.
 */
project.ext {
  jackrabbitJCAUri = 'http://www.silverpeas.org/files/jackrabbit-jca.rar'
  distDir = new File('dist', buildDir)
}

/**
 * Some configuration properties corresponding to this distribution.
 */
silversetup {
  driversDir = "${buildDir}/drivers"
}

/**
 * Cleans all the artifacts produced by the build of a Silverpeas distribution.
 */
clean {
  doLast {
    ant.delete(dir: "${silversetup.silverpeasHome}/log", includes: '*.log')
    ant.delete(dir: "${silversetup.silverpeasHome}/silvertrace", includeemptydirs: true)
    ant.delete(dir: "${silversetup.silverpeasHome}/migrations", includeemptydirs: true)
    ant.delete(dir: "${silversetup.silverpeasHome}/resources/StringTemplates", includeemptydirs: true)
    ant.delete(dir: "${silversetup.silverpeasHome}/properties", includeemptydirs: true,
        excludes: 'org/silverpeas/authentication/**/* org/silverpeas/domains/**/* org/silverpeas/util/viewGenerator/settings/**/*')
    ant.delete(dir: "${silversetup.silverpeasHome}/xmlcomponents", includeemptydirs: true,
        excludes: 'workflows/**/*')
  }
}

/**
 * Fetches the different parts of Silverpeas as well their dependencies from the remote Silverpeas
 * repository and prepares the build of the Silverpeas distribution. It is in this step the
 * Silverpeas application is assembled.
 */
assemble {
  outputs.dir distDir
  outputs.dir file(new File(project.silversetup.driversDir))
  outputs.file file(new File('silverpeas.war', buildDir))
  doFirst {
    // fetch all the artifacts and explode some of them into the output directories
    def artifacts = configurations.silverpeas.files
    artifacts.each { a ->
      if (a.name.endsWith('.war')) {
        copy {
          from(zipTree(a)) {
            rename 'web.xml', "web-${a.name}.xml"
          }
          into distDir.path
        }
      } else if (a.name.endsWith('-configuration.jar')) {
        copy {
          from(zipTree(a))
          exclude '**/META-INF/**'
          into project.silversetup.silverpeasHome
        }
      } else if (a.name.startsWith('postgresql') || a.name.startsWith('jtds')) {
        // h2 is already provided by JBoss >= 8
        copy {
          from(file(a)) {
            rename a.name, a.name.substring(0, a.name.indexOf('-')) + '.jar'
          }
          into project.silversetup.driversDir
        }
      }
    }
    ant.get(src: jackrabbitJCAUri, dest: buildDir.path)
  }
  doLast {
    // merge all of the web.xml from the different WARs into a single one
    def mainWebXmlFileName = "web-war-core-${project.version}.war.xml"
    def mainWebXmlFile = new File("${distDir.path}/WEB-INF/${mainWebXmlFileName}")
    def mainWebXml = new XmlSlurper(false, false).parse(mainWebXmlFile)
    new File('WEB-INF', distDir).listFiles(new FilenameFilter() {
      boolean accept(File f, String fileName) {
        return fileName.startsWith('web-') && fileName.endsWith('war.xml') &&
            fileName != mainWebXmlFileName
      }
    }).each {
      // the sub-elements of the web-app elements can be in the arbitrary order.
      def aWebXml = new XmlSlurper(false, false).parse(it)
      aWebXml.'context-param'.each { elt -> mainWebXml.appendNode(elt) }
      aWebXml.'filter'.each { elt -> mainWebXml.appendNode(elt) }
      aWebXml.'listener'.each { elt -> mainWebXml.appendNode(elt) }
      aWebXml.'servlet'.each { elt -> mainWebXml.appendNode(elt) }
      aWebXml.'servlet-mapping'.each { elt -> mainWebXml.appendNode(elt) }
      aWebXml.'resource-env-ref'.each { elt -> mainWebXml.appendNode(elt) }
      GFileUtils.forceDelete(it)
    }
    XmlUtil.serialize(mainWebXml, new FileWriter("${distDir.path}/WEB-INF/web.xml"))
    GFileUtils.forceDelete(mainWebXmlFile)
    ant.zip(destfile: "${buildDir.path}/silverpeas.war") {
      fileset(dir: distDir.path)
    }
  }
}

/**
 * Configures both the runtime (JBoss) and the Silverpeas distribution.
 */
task configure(dependsOn: [configureJBoss, configureSilverpeas]) {
  description 'Configures both JBoss and Silverpeas'
  group 'Build'
  doLast {
    println 'CONFIGURATION COMPLETE'
  }
}

/**
 * Deploys Silverpeas and some of its dependencies into the application server.
 */
task deploy(dependsOn: configure) {
  description 'Installs the Silverpeas application'
  group 'Silverpeas'
  doLast {
    copy {
      from files(new File('silverpeas.war', buildDir), new File('jackrabbit-jca.rar', buildDir))
      into "${project.silversetup.jbossHome}/standalone/deployments"
    }
  }
}

/**
 * Undeploys Silverpeas and some of its dependencies from the application server.
 */
task undeploy(type: Delete) {
  description 'Undeploy Silverpeas from the JBoss server (including Jackrabbit)'
  group 'Silverpeas'
  doLast {
    fileTree(dir: "${project.silversetup.jbossHome}/standalone/deployments").filter {
      it.name.startsWith('silverpeas.war') || it.name.startsWith('jackrabbit-jca.rar')
    }.each {
      delete it
    }
  }
}

/**
 * Starts Silverpeas.
 */
task start {
  description 'Starts Silverpeas'
  group 'Silverpeas'
  doLast {
    new JBossServer(project.silversetup.jbossHome)
        .redirectOutputTo(new File("${silversetup.logDir}/output.log"))
        .start()
  }
}

/**
 * Starts Silverpeas in debug mode.
 */
task debug {
  description 'Starts Silverpeas in debug mode'
  group 'Silverpeas'
  doLast {
    new JBossServer(project.silversetup.jbossHome)
        .redirectOutputTo(new File("${silversetup.logDir}/output.log"))
        .debug()
  }
}

/**
 * Stops Silverpeas.
 */
task stop {
  description 'Stops Silverpeas'
  group 'Silverpeas'
  doLast {
    new JBossServer(project.silversetup.jbossHome)
        .redirectOutputTo(new File("${silversetup.logDir}/output.log"))
        .stop()
  }
}

/**
 * Checks the status of the Silverpeas distribution.
 */
task status {
  description 'Checks the status of Silverpeas: is it running? is it well configured?'
  group 'Silverpeas'
  doLast {
    def jboss = new JBossServer(project.silversetup.jbossHome)
    String configurationStatus = (jboss.isAlreadyConfigured() ? 'Configured: [OK]' : 'Configured: [NOK]')
    String executionStatus = (jboss.isRunning() ? 'Running:    [OK]' : 'Running:    [NOK]')
    println configurationStatus
    println executionStatus
  }
}




